buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'com.novoda:bintray-release:0.3.4'
        // clean build bintrayUpload
    }
}

ext {
    configuration = [
            compileVersion   : 23,
            buildToolsVersion: "23.0.2",
            minSdk           : 7,
            targetSdk        : 23,
            versionCode      : 4,
            versionName      : "1.1.2"
    ]

    bintrayConfig = [
            userOrg            : "blipinsk",
            userName           : "Bartosz Lipinski",
            userEmail          : "bartoszlipinski90@gmail.com",
            groupId            : "com.bartoszlipinski",
            name               : "XmlTag",
            artifactLibrary    : "xmltag",
            artifactCompiler   : "xmltag-compiler",
            artifactAnnotations: "xmltag-annotations",
            description        : "The easiest way to simplify custom View tags in Android layout xmls.",
            website            : "https://github.com/blipinsk/XmlTag",
            gitUrl             : "https://github.com/blipinsk/XmlTag.git",
            licenses           : ["Apache-2.0"],
            licenseDesc        : "The Apache Software License, Version 2.0"
    ]

    bintrayUser = BINTRAY_USERNAME
    bintrayKey = BINTRAY_API_KEY
    dryRun = 'true'
    isUploading = project.getGradle().startParameter.taskNames.any { it.contains('bintrayUpload') }
}

subprojects {
    repositories {
        jcenter()
    }

    group = bintrayConfig.groupId
    version = configuration.versionName

    if (isUploading && project.name in [bintrayConfig.artifactLibrary, bintrayConfig.artifactCompiler, bintrayConfig.artifactAnnotations]) {
        println project.name
        apply plugin: 'maven'

        gradle.taskGraph.whenReady { taskGraph ->
            taskGraph.getAllTasks().find {
                it.path == ":$project.name:generatePomFileForMavenPublication"
            }.doLast {
                file("build/publications/maven/pom-default.xml").delete()
                pom {
                    //noinspection GroovyAssignabilityCheck
                    project {
                        name project.name
                        artifactId project.name
                        packaging project.name ==
                                bintrayConfig.artifactCompiler || project.name == bintrayConfig.artifactAnnotations ? 'jar' : 'aar'
                        description bintrayConfig.description
                        url bintrayConfig.website
                        version configuration.versionName

                        scm {
                            url bintrayConfig.gitUrl
                            connection bintrayConfig.gitUrl
                            developerConnection bintrayConfig.gitUrl
                        }

                        licenses {
                            license {
                                name bintrayConfig.licenseDesc
                            }
                        }

                        developers {
                            developer {
                                id bintrayConfig.userOrg
                                name bintrayConfig.userName
                                email bintrayConfig.userEmail
                            }
                        }
                    }
                }.writeTo("build/publications/maven/pom-default.xml")
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}